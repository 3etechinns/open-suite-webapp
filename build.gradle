apply plugin: 'axelor-app'

def modules = new File('modules/abs').listFiles().findAll { dir ->
	new File(dir, 'build.gradle').exists() && !dir.name.endsWith('business-production') && !dir.name.endsWith('organisation')
}

def installedModules = new File('modules/abs').listFiles().findAll { dir ->
	new File(dir, 'build.gradle').exists() && !dir.name.endsWith('-demo-fr') && !dir.name.endsWith('-l10n-fr') && !dir.name.endsWith('business-production')
}

application {

	name "axelor-erp"
	title "Axelor :: ERP"
	
	version "3.0.0"
	
	// ADK version required
	adkVersion "~3.0.0"

	// depends on all modules
	modules.each { dir ->
		module "modules:$dir.name"
	}
	
	// install all modules
	installedModules.each { dir ->
			install "$dir.name"
	}
	
}

tomcat {
	httpPort = project.properties.get("http.port", 8080) as Integer
	stopPort = 9451
}

task("dataImport", type: JavaExec) {
	main = "com.axelor.apps.erp.Main"
	classpath = sourceSets.main.runtimeClasspath
	if(System.getProperty("exec.args") != null) {
		args System.getProperty("exec.args").split()
	}
}

build.doLast{
	archiveReports.execute()
}

task archiveReports(type: Zip){
	from 'modules/abs'
    include '*/src/main/resources/reports/*'
	classifier 'reports'
	eachFile { fileCopyDetails ->
		fileCopyDetails.path = fileCopyDetails.path.replaceAll(".*/src/main/resources/reports/", '')
	}
	includeEmptyDirs = false
	destinationDir = new File("$buildDir/libs")
}
